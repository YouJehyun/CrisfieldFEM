      SUBROUTINE ELSTRUC(AKTE,AKTS,NV,AK15,ID14S,AK14S,NDSP,FI,PT,
     1                    IMOD,IWRIT,IWR)
C
C     FOR IMOD=2 OR 3
C     PUTS EL-STIFF MATRIX AKTE(4,4) INTO STRUCT.STIFF AKTS(NV,NV)
C     IF NV = 5, ALSO ADDS IN LINEAR SPRING AK15 BETWEEN VARBLS.1&5
C     ALSO ADDS IN NDSP EARTHED LINEAR SPRINGS FOR VARBLS.1-4
C     USING PROPERTIES IN AK14S(4) AND DEGS.OF F.IN IDSPS(4)
C     THROUGHOUT ONLY WORKS WITH UPPER TRIANGLE
C     FOR IMOD=1 OR 3
C     MODIFIES INTERNAL FORCE VECT., FI TO INCLUDE EFFECTS FROM
C     ARIOUS LINEAR SPRINGS USING TOTAL DISPS., PT.
C
      DOUBLE PRECISION AKTE(4,4),AKTS(NV,NV),FI(NV),PT(NV),AK14S(4),
     1                 AK15
      INTEGER  ID14S(4),NV,NDSP,IMOD,IWRIT,IWR,I,J
C
      IF (IMOD.NE.2) THEN
C     MODIFY FORCES
        IF (INDSP.NE.0) THEN
C     FOR EARTHED SPRINGS
          DO 40 I=1,NDSP
            IDS = ID14S(I)
            FI(IDS) = FI(IDS) + AK14S(I)*PT(IDS)
   40     CONTINUE
        ENDIF
C
        IF (IWRIT.NE.0) WRITE (IWR,1002) FI
 1002   FORMAT(/,1X,'STR.INT.FORCE VECT IS',1X,5G13.5,/)
C
      ENDIF
C
      IF (IMOD.NE.1) THEN
C     WORK ON STIFFNESS MATRIX; CLEAR STRUCT.STIFFNESS MATRIX
        DO 10 I=1,NV
          DO 11 J=1,NV
            AKTS(I,J) = 0.D0
   11     CONTINUE
   10   CONTINUE
C
C     INSERT EL.STIFFNESS MATRIX
        DO 20 I=1,4
          DO 21 J=1,4
            AKTS(I,J) = AKTE(I,J)
   21     CONTINUE
   20   CONTINUE
C
C     SPRING BETWEEN VARBLS.1&5
        IF (NV.EQ.5) THEN
          AKTS(1,1) = AKTS(1,1) + AK15
          AKTS(1,5) = AKTS(1,5) - AK15
          AKTS(5,5) = AKTS(5,5) + AK15
        ENDIF
C
C       EARTHED SPRINGS FOR VARBLS.1-4
        IF (NDSP.NE.0) THEN
          DO 30 I=1,NDSP
            IDS = ID14S(I)
            AKTS(IDS,IDS) = AKTS(IDS,IDS) + AK14S(I)
   30     CONTINUE
        ENDIF
C
        IF (IWRIT.NE.0) THEN
          WRITE (IWR,1001)
 1001     FORMAT(/,1X,'FULL STRUCT.TAN.STIFF.IS',/)
          DO 50 I=1,NV
            WRITE (IWR,67) (AKTS(I,J), J=1,NV)
   67     FORMAT(1X,7G13.5)
   50     CONTINUE
        ENDIF
C
      ENDIF
C
      RETURN
      END